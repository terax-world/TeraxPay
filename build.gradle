plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'world.terax'
version = '1.0-SNAPSHOT'

def targetJavaVersion = 8

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT'

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation ('redis.clients:jedis:5.1.0') {
        exclude group: 'org.slf4j'
    }

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveBaseName.set('TeraxPay')
    archiveVersion.set('')
    archiveClassifier.set('')
    relocate 'redis.clients', 'world.terax.libs.redis'
    mergeServiceFiles()
    minimize {
        exclude(dependency('redis.clients:jedis'))
    }
}

jar {
    enabled = false
}

task copyJar(type: Copy) {
    dependsOn shadowJar
    from 'build/libs/TeraxPay.jar'
    into 'D:/.terax.world/servers/dev01/plugins'
}

build.dependsOn shadowJar
build.finalizedBy copyJar
